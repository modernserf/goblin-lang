import [_File_ _Log_ _Vec_ _Parser_ _Panic_] := "core"

let result_scores := [win: 6 draw: 3 lose: 0]
let shape_scores := [rock: 1 paper: 2 scissors: 3]
let rules := Vec{}
  , [a: [rock] b: [rock] c: [draw]]
  , [a: [rock] b: [paper] c: [lose]]
  , [a: [rock] b: [scissors] c: [win]]
  , [a: [paper] b: [rock] c: [win]]
  , [a: [paper] b: [paper] c: [draw]]
  , [a: [paper] b: [scissors] c: [lose]]
  , [a: [scissors] b: [rock] c: [lose]]
  , [a: [scissors] b: [paper] c: [win]]
  , [a: [scissors] b: [scissors] c: [draw]]

# this is fast enough, but would be nice if this were indexed
let query := [
  on {player: a opponent: b}
    rules{find: {: row}
      (row{a} = a) && (row{b} = b)
    }{some!}{c}
  on {opponent: b result: c}
    rules{find: {: row}
      (row{b} = b) && (row{c} = c)
    }{some!}{a}
]

# TODO: maybe Parser{try match: f} -> Option{value}
let left_hand := Parser{any} |>> [{: ch}
  if ch = "A" then 
    [rock]
  else if ch = "B" then
    [paper]
  else if ch = "C" then
    [scissors]
  else
    Panic{message: "unexpected char"}
  end
]

let right_hand := Parser{any} |>> [{: ch}
  if ch = "X" then 
    [rock]
  else if ch = "Y" then
    [paper]
  else if ch = "Z" then
    [scissors]
  else
    Panic{message: "unexpected char"}
  end
]

let right_result := Parser{any} |>> [{: ch}
   if ch = "X" then 
      [lose]
    else if ch = "Y" then
      [draw]
    else if ch = "Z" then
      [win]
    else
      Panic{message: "unexpected char"}
    end
]

let space := Parser{token: " "}

let round_1 :=
  left_hand .>> space .>>. right_hand .>> Parser{newline} |>> [{:[0: opponent 1: player]}
    let result := query{player: player opponent: opponent}
    player{: shape_scores} + result{: result_scores}
  ]

let round_2 :=
  left_hand .>> space .>>. right_result .>> Parser{newline} |>> [{:[0: opponent 1: result]}
    let player := query{opponent: opponent result: result}
    player{: shape_scores} + result{: result_scores}
  ]

let sum := [{: xs} xs{into: 0 fold: {: x into: sum} sum + x}]

let input := File{read text sync: "./aoc-2022/day-2.input.txt"}
let part_1 := Parser{parse: input with: *round_1 |>> sum}{some!}
Log{: "part 1: " ++ part_1{to String}}
let part_2 := Parser{parse: input with: *round_2 |>> sum}{some!}
Log{: "part 2: " ++ part_2{to String}}